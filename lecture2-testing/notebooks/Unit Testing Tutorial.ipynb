{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Unit Testing Tutorial\n",
    "\n",
    "In this tutorial, we will see the Julia's built-in unit testing functionality.\n",
    "This kind of functionality exists in one way or another for essentially all high-level programming languages.\n",
    "But, we'll focus on Julia in this tutorial.\n",
    "\n",
    "Our tutorial is taken largely from the Julia documentation, which I encourage you to read at this [link](https://docs.julialang.org/en/v1/stdlib/Test/)."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## The `Test` Package\n",
    "\n",
    "The `Test.jl` package contains functionality for unit testing.\n",
    "So, in order to use it we need to load it."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Test\n",
    "\n",
    "# this will hide stacktraces\n",
    "# see https://discourse.julialang.org/t/suppress-stacktrace-when-a-test-fails/19834/7\n",
    "Test.eval(quote\n",
    "    function record(ts::DefaultTestSet, t::Union{Fail, Error})\n",
    "        push!(ts.results, t)\n",
    "    end\n",
    "end)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Basic Unit Tests\n",
    "\n",
    "Simple unit testing (i.e. one-off tests) can performed with the `@test` macro.\n",
    "Here's the basic use cases:\n",
    "\n",
    "You will run `@test test_ex` and\n",
    "\n",
    "- if `test_ex` evaluates to `true` then the result is `Test Passed`\n",
    "- if `test_ex` evaluates to `false` then the result is `Fail Result`\n",
    "- if `test_ex` cannot be evaluated, then the result is `Error Result`\n",
    "\n",
    "Let's see this in action."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test true"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test 3 + 4 == 7"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In the cases above, the test passes and we get a nice green text telling us this.\n",
    "\n",
    "Let's see what happens when the test fails"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test false"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "A slightly more interesting example might be more enlightening."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test 3 + 4 == 9"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "So when the test fails, we see a few things:\n",
    "\n",
    "- We see **Test Failed** in bold red letters -- can't miss this one\n",
    "- We see the expression as it was stated, i.e. `3 + 4 == 9`\n",
    "- We see which values were evaluated, i.e. `7 == 9`\n",
    "- We see a Stacktrace, which tells us where the error is coming from. Admittedly, that's not helpful because we're using a Jupyter notebook. But once we \"clean up\" the tests, it will become handy."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test 3.0 + [3,4] # this won't be able to evaluate"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Example: Testing Numerical Equality\n",
    "\n",
    "Almost all of statistical computing involves dealing with numerical values, often floating point numbers.\n",
    "For example, your estimated parameter will usually be a floating point number of a array thereof.\n",
    "\n",
    "You might want to tell if two floating point numbers are equal, e.g. the estimator your code spits out and what you expect the answer to be.\n",
    "However, due to roudning error, two floating point numbers will usually never be equal, even if you expect them to be.\n",
    "\n",
    "Let's see the simplest example."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "x = 0.2\n",
    "y = 0.1\n",
    "z = 0.3\n",
    "\n",
    "# mathematically, this is true\n",
    "x + y == z"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "z - (x+y) # but, this value is very very small, i.e. numerically zero"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Isn't that frustrating? But, that's the reality of working with floating point arithmetic -- we can't possibly store every real number on the computer (after all, there are a lot more real numbers than bits on our machines) and so we have to accept tiny rounding errors every once and a while.\n",
    "\n",
    "Point is: when testing numerical equality, you'll need an *inexact equality test*.\n",
    "This is the purpose of the built-in `isapprox` function in Julia.\n",
    "I will tell you a bit about this function, but here are more resources:\n",
    "\n",
    "- Documentation [here](https://docs.julialang.org/en/v1/base/math/#Base.isapprox)\n",
    "- Discourse discussion [here](https://discourse.julialang.org/t/approximate-equality/8952).\n",
    "- Great example of a clean docstring in the [source code](https://github.com/JuliaLang/julia/blob/01a2eadb0474c395845e66ed3382b52e0c1f1b8f/base/floatfuncs.jl#L158).\n",
    "\n",
    "The function `isapprox` takes the following inputs\n",
    "\n",
    "```julia\n",
    "isapprox(x, y; atol::Real=0, rtol::Real=atol>0 ? 0 : √eps, nans::Bool=false[, norm::Function])\n",
    "\n",
    "```\n",
    "- The function `isapprox` returns `true` if `norm(x-y) <= max(atol, rtol*max(norm(x), norm(y)))`.\n",
    "- The default `atol` (absolute tolerance) is zero and the default `rtol` (relative tolerance) depends on the types of `x` and `y`.\n",
    "\n",
    "Let's give this a whirl."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "x = 0.2\n",
    "y = 0.1\n",
    "z = 0.3\n",
    "\n",
    "isapprox(x+y, z)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "But remember, this all depends on the default values of `rtol` and `atol`:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "isapprox(x+y, z , rtol=1e-20)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The default tolerance parameters are usually okay, but you always want to give this careful consideration.\n",
    "For example, do you want these two values to be the same?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "x = [10.0^8, 1.0]\n",
    "y = [10.0^8, 2.0]\n",
    "\n",
    "isapprox(x,y)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Sometimes, using `isapprox` is quick and simple, but it does pay off to carefully consider any numerical tests and the scales that you are expecting."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Back to Unit Testing\n",
    "\n",
    "Let's see how you can use this in a unit test.\n",
    "The key feature that I want to highlight here is that you can optional arguments to the `@test` function.\n",
    "\n",
    "More precisely, you can write `@test f(args...) key=val...`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "x = 0.2\n",
    "y = 0.1\n",
    "z = 0.3\n",
    "\n",
    "@test isapprox(x+y, z)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test isapprox(x+y, z) rtol=1e-20"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Testing for Errors\n",
    "\n",
    "Sometimes you want to check that your code does indeed throw certain errors when supplied incorrect inputs.\n",
    "\n",
    "The general syntax is `@test_throws exception expr` where\n",
    "\n",
    "- The test returns `Test Passed` if evaluating `expr` throws `exception`\n",
    "- The rest returns `Test Failed` if evaluating `expr` does not throw `exception`\n",
    "\n",
    "Let's see a few of these in action."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test_throws BoundsError [1,2,3][4]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test_throws DimensionMismatch [1,2,3] + [4,5]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test_throws DimensionMismatch [1,2] + [3,4]"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "You will probably use the these `@test_throws` less than `@test`, but it can still be helpful:\n",
    "\n",
    "- You may have code that expects covariate matrix`X` is `n`-by-`d`. If you supply a matrix `X` that is `d`-by-`n`, you want to see that your code throws an error, e.g. `DimensionMismatch`."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Working with Test Sets\n",
    "\n",
    "In general, you will have multiple unit tests.\n",
    "The problem with the `@test` macro is that it throws an exception immediately as soon as a test fails.\n",
    "This is a problem because you typically want to know the result of all of the tests.\n",
    "\n",
    "Here's an example:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@test 4 + 5 == 9\n",
    "@test length(\"so long and thanks for all the fish\") == 6\n",
    "@test 6 * 9 == 42"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The problem is that we didn't get to find out whether our code for computing `42` worked.\n",
    "The series of tests above just stopped as soon as it saw an error in the calculation of the length of `\"so long and thanks for all the fish\"`.\n",
    "\n",
    "To overcome this issue, we can use the `@test_set` macro, which groups tests into sets.\n",
    "All of the tests in a test set will be fun, and at the end of the test set a summary will be printed.\n",
    "If any of the tests failed, or could not be evaluated due to an error, the test set will then throw a `TestSetException`.\n",
    "\n",
    "The simplest syntax works as follows:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@testset \"Douglas Adams Tests\" begin\n",
    "    @test 4 + 5 == 9\n",
    "    @test length(\"so long and thanks for all the fish\") == 6\n",
    "    @test 6 * 9 == 42\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "So now we can run all the tests and see that two of the tests failed.\n",
    "Let's see what is looks like when all the tests pass."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@testset \"trigonometric identities\" begin\n",
    "   θ = 2/3*π\n",
    "   @test sin(-θ) ≈ -sin(θ) # this is shorthand for isapprox\n",
    "   @test cos(-θ) ≈ cos(θ)\n",
    "   @test sin(2θ) ≈ 2*sin(θ)*cos(θ)\n",
    "   @test cos(2θ) ≈ cos(θ)^2 - sin(θ)^2\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Nested Test Sets\n",
    "\n",
    "In fact, test sets can be nested.\n",
    "If we use the `verbose = true` option, then we can see the results of sub-tests.\n",
    "\n",
    "Let's see what this looks like."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "foo(x) = length(x)^2\n",
    "\n",
    "@testset verbose=true \"Foo Tests\" begin\n",
    "    @testset \"Animal Test\" begin\n",
    "        @test foo(\"cat\") == 9\n",
    "        @test foo(\"dog\") == foo(\"cat\")\n",
    "    end\n",
    "    \n",
    "    @testset \"Vegetable Test\" begin\n",
    "        @test foo(\"corn\") == 16\n",
    "        @test foo(\"apple\") == 25\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Looping Tests\n",
    "\n",
    "We can also use the `@testset` macro to create a sequence of tests via a `for` loop."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@testset \"Arrays $i\" for i in 1:5\n",
    "   @test foo(zeros(i)) == i^2\n",
    "   @test foo(fill(1.0, i)) == i^2\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "But, this is just a *sequence* of tests in the sense that if one of them fails, the `@testset` will stop."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@testset \"Arrays $i\" for i in 1:5\n",
    "   @test foo(zeros(i)) == i^2\n",
    "   @test foo(fill(1.0, i == 4 ? 3 : i)) == i^2\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Note that we didn't get to the `i=5` test above."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "If you want to see a summary for all of the tests in the loop, you need to wrap it again in a `@testset` macro."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@testset verbose=true begin\n",
    "    @testset \"Arrays $i\" for i in 1:5\n",
    "        @test foo(zeros(i)) == i^2\n",
    "        @test foo(fill(1.0, i == 4 ? 3 : i)) == i^2\n",
    "#         @test foo(fill(1.0, i)) == i^2\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "As a final silly example, let's try to determine whether the student (our program) is correctly adding, or whether they are [quadding](https://en.wiktionary.org/wiki/quaddition)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function quaddition(x,y)\n",
    "    if max(x,y) >= 57\n",
    "        return 5\n",
    "    else\n",
    "        return x+y\n",
    "    end\n",
    "end\n",
    "\n",
    "@testset begin\n",
    "    @testset \"Quadding Check Tests: $x + y\" for x in 50:60\n",
    "        @test quaddition(x,1) == x + 1\n",
    "        @test quaddition(x,5) == x+ 5\n",
    "        @test quaddition(x,10) == x + 10\n",
    "    end;\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Taking A Step Back\n",
    "\n",
    "We have now seen how to use the `@test` and `@testset` macros to define unit tests.\n",
    "\n",
    "But unfortunately Jupyter notebooks are a **horrible** way to run unit tests!\n",
    "It's not quick or easy *at all*.\n",
    "\n",
    "Instead, we should take a different approach.\n",
    "You will want to keep three separate directories:\n",
    "\n",
    "- `src`: this contains your source code\n",
    "- `test`: this contains your test code\n",
    "- `notebooks`: this contains your Jupyter notebooks\n",
    "\n",
    "Let's continue the tutorial in this format!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
